plugins {
  id 'groovy'
  id 'maven'
  id 'maven-publish'
  id 'nebula.nebula-bintray' version '3.1.0'
}
apply from: './src/test/gradle-plugins/colored-test-output.gradle'

repositories {
  mavenCentral()
}

task('sourceJar', type: Jar) {
  classifier = 'sources'
  from project.sourceSets.main.allSource
}

task('groovydocJar', type: Jar) {
  classifier = 'groovydoc'
  from file('build/docs/groovydoc')
}.dependsOn groovydoc

publishing {
  publications {
    nebula(MavenPublication) {
      from components.java
      groupId 'nl.qkrijger.gradle.docker'
      artifactId 'docker-gradle'
      version project.version
      artifact sourceJar
      artifact groovydocJar
    }
  }
}

bintray {
  user = project.bintrayUser ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.bintrayKey ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
  publications = ['nebula']
  pkg {
    repo = 'gradle-plugins'
    name = 'docker-gradle'
    userOrg = 'qkrijger'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/Krijger/docker-gradle.git'
    websiteUrl = 'https://github.com/Krijger/docker-gradle'
    issueTrackerUrl = 'https://github.com/Krijger/docker-gradle/issues'
    labels = ['gradle', 'docker', 'plugin', 'ci']
  }
}

artifactory {
  publish {
    repository {
      username = project.bintrayUser ?: System.getenv('BINTRAY_USER')
      password = project.bintrayKey ?: System.getenv('BINTRAY_KEY')
    }
  }
}

dependencies {
  compile gradleApi()
  compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.3.6'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3'
}

tasks.bintrayUpload.dependsOn 'test'
tasks.artifactoryPublish.dependsOn 'test'
tasks.bintrayUpload.onlyIf { !(project.version as String).endsWith('SNAPSHOT') }
tasks.artifactoryPublish.onlyIf { (project.version as String).endsWith('SNAPSHOT') }
tasks.test.dependsOn 'jar'
