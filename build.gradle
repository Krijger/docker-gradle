buildscript { repositories { jcenter() } } // Some plugin dependencies (namely JFrog) are not in Maven Central

plugins {
  id 'groovy'
  id 'maven'
  id 'nebula.info' version '2.2.4'
  id 'nebula.contacts' version '2.2.0'
  id 'nebula.javadoc-jar' version '4.0.1'
  id 'nebula.source-jar' version '4.0.1'
  id 'nebula.maven-apache-license' version '4.0.1'
  id 'nebula.maven-publish' version '4.2.1'
  id 'nebula.nebula-release' version '3.0.2'
  id 'nebula.nebula-bintray' version '3.1.0'
}
apply from: './src/test/gradle-plugins/colored-test-output.gradle'

group = 'nl.qkrijger.gradle'

contacts {
  'qkrijger@gmail.com' {
    moniker 'Quinten Krijger'
    github 'krijger'
  }
  'wouter.danes@gmail.com' {
    moniker 'Wouter Danes'
    github 'wouterd'
  }
  'ernst.naezer@gmail.com' {
    moniker 'Ernst Naezer'
    github 'ernstnaezer'
  }
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile group: 'org.yaml', name: 'snakeyaml', version: '1.16'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3'
}

repositories {
  jcenter()
}

bintray {
  user = project.bintrayUser ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.bintrayKey ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${project.buildDir}/repoForTests")
      pom.version = '0.1-TEST'
    }
  }
}
tasks.test.dependsOn tasks.uploadArchives
