buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'groovy'
  id 'maven'

  id 'nebula.info' version '2.2.4'
  id 'nebula.contacts' version '2.2.0'
  id 'nebula.maven-apache-license' version '4.0.1'

  id 'nebula.maven-publish' version '4.0.1'
  id 'nebula.javadoc-jar' version '4.0.1'
  id 'nebula.source-jar' version '4.0.1'

  id 'nebula.nebula-release' version '3.0.2'

  id 'nebula.nebula-bintray' version '3.1.0'

  id "com.gradle.plugin-publish" version "0.9.2"
}
apply from: './src/test/gradle-plugins/colored-test-output.gradle'

group = 'org.stackwork.gradle'

contacts {
  'qkrijger@gmail.com' {
    moniker 'Quinten Krijger'
    github 'krijger'
  }
  'wouter.danes@gmail.com' {
    moniker 'Wouter Danes'
    github 'wouterd'
  }
  'ernst.naezer@gmail.com' {
    moniker 'Ernst Naezer'
    github 'ernstnaezer'
  }
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile group: 'org.yaml', name: 'snakeyaml', version: '1.16'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3'
}

/**
 * Configuration for publishing to Bintray: https://bintray.com/stackwork/gradle-plugins/stackwork/
 */
bintray {
  pkg {
    userOrg = 'stackwork'
    repo = 'gradle-plugins'
    name = 'stackwork'
    desc = project.description
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/stackwork/stackwork.git'
    websiteUrl = 'https://www.stackwork.org'
    issueTrackerUrl = 'https://github.com/stackwork/stackwork/issues'
    labels = ['gradle', 'docker', 'plugin', 'ci', 'stackwork', 'docker-compose']
  }
}

/**
 * Configuration for publishing to Gradle Plugins: https://plugins.gradle.org/plugin/org.stackwork
 */
pluginBundle {
  website = 'https://www.stackwork.org'
  vcsUrl = 'https://github.com/stackwork/stackwork.git'
  description = 'Full stack testing with Docker Compose'
  tags = ['gradle', 'docker', 'plugin', 'ci', 'stackwork', 'docker-compose']

  plugins {
    stackworkPlugin {
      id = 'org.stackwork'
      displayName = 'Stackwork Gradle plugin'
    }
  }
}

/**
 * Upload the build result to a local repositories that is used in the tests
 */
uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${project.buildDir}/repoForTests")
      pom.version = '0.1-TEST'
    }
  }
}
tasks.test.dependsOn tasks.uploadArchives
