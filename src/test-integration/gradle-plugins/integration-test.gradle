import org.gradle.logging.StyledTextOutputFactory
import static org.gradle.logging.StyledTextOutput.Style

// Assumes 'groovy' or 'scala' plugins have been applied before

// Add integration test source sets
sourceSets {
  integrationTest { sourceSet ->
    ["groovy", "scala", "resources"].each {
      if (!sourceSet.hasProperty(it)) return
      sourceSet."$it".srcDir file("src/test-integration/${it}")
    }
  }
}

// Setup dependencies for integration testing
dependencies {
  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
}

// Define integration test task
task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath

  def out = services.get(StyledTextOutputFactory).create("colored-test-output")
  out.style Style.Normal

  beforeSuite { suite ->
    if (suite.name.startsWith("Test Run") || suite.name.startsWith("Gradle Worker")) return
    out.println "\n${suite.name}"
  }
  afterTest { descriptor, result ->
    def style
    if (result.failedTestCount > 0) style = Style.Failure
    else if (result.skippedTestCount > 0) style = Style.ProgressStatus
    else style = Style.Success

    out.text('  ').withStyle(style).println descriptor.name
  }
}

// Make sure 'check' task calls integration test
check.dependsOn integrationTest
integrationTest.dependsOn jar
