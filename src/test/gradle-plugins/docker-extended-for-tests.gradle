apply plugin: 'groovy'
apply plugin: 'docker'

repositories {
  jcenter()
}

def travisBuild = System.getenv('TRAVIS_BRANCH') != null
logger.info "Running on Travis: '$travisBuild'"

docker {
  stopContainers = !travisBuild
}

task('cleanup', description: 'Remove the test docker image by id, even when it is tagged', type: Exec) {
  commandLine 'docker', 'rmi', "${-> project.docker.imageId}"
}.onlyIf { extensions.docker.stopContainers }

tasks.buildImage.finalizedBy tasks.cleanup
tasks.runDockerCompose.finalizedBy tasks.cleanDockerCompose
// different test cases call different tasks. Cleanup will run as soon as buildImage is used, but it should happen
// after a test task dependent on the image existing has run
tasks.cleanup.mustRunAfter tasks.check, tasks.tagImage, tasks.pushImage
tasks.cleanup.mustRunAfter tasks.runDockerCompose, tasks.stopDockerCompose, tasks.cleanDockerCompose
