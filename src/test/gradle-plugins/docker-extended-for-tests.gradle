apply plugin: 'docker'

allprojects {
  repositories {
    jcenter()
  }
}

def travisBuild = System.getenv('TRAVIS_BRANCH') != null
logger.info "Running on Travis: '$travisBuild'"

allprojects {
  docker {
    stopContainers = !travisBuild
  }
}

allprojects { Project currentProject ->

  afterEvaluate {
    if (!currentProject.tasks.hasProperty('check')) {
      currentProject.task('check').dependsOn currentProject.tasks.dockerCheck
    }
  }

  afterEvaluate {
    Task cleanupTask = currentProject.task('cleanup', description: 'Remove the test docker image by id,', type: Exec) {
      commandLine 'docker', 'rmi', "-f" , "${-> currentProject.docker.imageId}"
    }
    cleanupTask.onlyIf {
      currentProject.extensions.docker.stopContainers && currentProject.tasks.buildImage.didWork
    }
    currentProject.tasks.buildImage.finalizedBy cleanupTask

    cleanupTask.mustRunAfter currentProject.tasks.dockerTest

    Project parentOrSelf = currentProject.parent ?: currentProject
    parentOrSelf.allprojects { otherProject ->
      otherProject.getAllTasks(true).values().each { taskSet ->
        taskSet.findAll({ task -> task.group == 'Docker' || task.name == 'test' }).each { dockerTask ->
          cleanupTask.mustRunAfter dockerTask
        }
      }
    }
  }
}

