apply plugin: 'docker'

allprojects {
  repositories {
    jcenter()
  }
}

def travisBuild = System.getenv('TRAVIS_BRANCH') != null
logger.info "Running on Travis: '$travisBuild'"

allprojects {
  docker {
    stopContainers = !travisBuild
  }
}
afterEvaluate {
  allprojects { currentProject ->

    if (!currentProject.tasks.hasProperty('check')) {
      currentProject.task('check').dependsOn currentProject.tasks.dockerCheck
    }

    Task cleanupTask = currentProject.task('cleanup', description: 'Remove the test docker image by id,', type: Exec) {
      commandLine 'docker', 'rmi', "-f" , "${-> currentProject.docker.imageId}"
    }
    cleanupTask.onlyIf {
      if (!currentProject.extensions.docker.stopContainers || !currentProject.tasks.buildImage.didWork) {
        return false
      }
      def imageId = currentProject.docker.imageId
      def imageExists = project.exec {
        setCommandLine(['docker', 'inspect', imageId])
        setStandardOutput(new ByteArrayOutputStream())
        setIgnoreExitValue(true)
      }.exitValue == 0
      imageExists
    }
    currentProject.tasks.buildImage.finalizedBy cleanupTask

    currentProject.rootProject.getAllTasks(true).values().flatten().findAll({ task -> task.group == 'Docker' }).each {
      cleanupTask.mustRunAfter it
    }
  }
}
