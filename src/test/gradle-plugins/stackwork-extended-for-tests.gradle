apply plugin: 'stackwork'

allprojects {
  repositories {
    jcenter()
  }
}

def travisBuild = System.getenv('TRAVIS_BRANCH') != null
logger.info "Running on Travis: '$travisBuild'"

afterEvaluate {
  allprojects { currentProject ->

    if (!currentProject.tasks.hasProperty('check')) {
      currentProject.task('check').dependsOn currentProject.tasks.stackworkCheck
    }

    Task cleanupTask = currentProject.task('cleanup', description: 'Remove the test docker image by id,', type: Exec) {
      executable 'docker'
      args "rmi", "-f" , "${-> currentProject.stackwork.imageId}"
    }
    cleanupTask.onlyIf {
      if (!currentProject.extensions.stackwork.stopContainers || !currentProject.tasks.buildImage.didWork) {
        return false
      }
      def imageId = currentProject.stackwork.imageId
      def imageExists = project.exec {
        setCommandLine(['docker', 'inspect', imageId])
        setStandardOutput(new ByteArrayOutputStream())
        setIgnoreExitValue(true)
      }.exitValue == 0
      imageExists
    }
    currentProject.tasks.buildImage.finalizedBy cleanupTask

    currentProject.rootProject.getAllTasks(true).values().flatten().findAll({ task -> task.group == 'Stackwork' }).each {
      cleanupTask.mustRunAfter it
    }
  }

  allprojects {
    project.extensions.findByName('stackwork').imageBuildDependencies.each {
      it.tasks.cleanup.dependsOn project.tasks.cleanup
    }
  }
}
